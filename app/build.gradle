apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "org.dslul.openboard.inputmethod.latin"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 14
        versionName '1.4.2'
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    aaptOptions {
        noCompress 'dict'
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    lintOptions {
        abortOnError false
    }

    ndkVersion '21.3.6528147'

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def project = "openboard"
            def apkExtension = ".apk"
            def SEP = "_"
            def buildType = name
            def versionName = variant.versionName
            def versionCode = versionCode
            def date = new Date()
            def formattedDate = date.format('ddMMyy_HHmm')
            def newApkName = project + SEP + buildType +
                    SEP + versionCode + SEP + versionName +
                    SEP + formattedDate + apkExtension
            outputFileName = new File(newApkName)
        }
    }
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
}
repositories {
    mavenCentral()
}